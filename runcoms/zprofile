#
# Executes commands at login pre-zshrc.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

# Set the SHELL for login shells to be the currently running zsh
# Note that 'which' works with full paths, too.
# The substitution takes off any leading -
export SHELL=$(which ${0#-})

#
# Browser
#

if [[ "$OSTYPE" == darwin* ]]; then
  export BROWSER='open'
fi

#
# Editors
#

if (( $+commands[emacs] )); then
    # In the emacs-client module
    export VISUAL=ec
    export GIT_EDITOR=$(which vim)
else
    export VISUAL=$(which vim)
fi
export EDITOR=$VISUAL
export FCEDIT=$VISUAL

export PAGER='less'

#
# Language
#

if [[ -z "$LANG" ]]; then
  export LANG='en_US.UTF-8'
fi

#
# Paths
#

# Ensure path arrays do not contain duplicates.
typeset -gU cdpath fpath mailpath path

# Set the list of directories that cd searches.
# cdpath=(
#   $cdpath
# )

# Set the list of directories that Zsh searches for programs.
path=(
  /usr/local/{bin,sbin}
  $path
)

#
# This allows us to say HEAD^ for Git without having to quote it
#

unsetopt EXTENDED_GLOB

#
# Less
#

# Set the default Less options.
# Mouse-wheel scrolling has been disabled by -X (disable screen clearing).
# Remove -X and -F (exit if the content fits on one screen) to enable it.
export LESS='-F -g -i -M -R -S -w -X -z-4'

# Set the Less input preprocessor.
# Try both `lesspipe` and `lesspipe.sh` as either might exist on a system.
if (( $#commands[(i)lesspipe(|.sh)] )); then
  export LESSOPEN="| /usr/bin/env $commands[(i)lesspipe(|.sh)] %s 2>&-"
fi

# From old customized functions

setenv () { export $1=$2; }

# From old .zshenv

export PIP_DOWNLOAD_CACHE=$HOME/.pip_download_cache

[ -d $HOME/bin ] && export PATH=$HOME/bin:$PATH

# Add git if installed on Mac
# Interactive only, to make sure that it doesn't interfere with Xcode
[[ -o interactive ]] && [ -x /usr/local/git/bin/git ] && export PATH=/usr/local/git/bin:$PATH

# CVS
export CVSROOT=:pserver:kam@cvs.dlogics.com:/dl/cvsroot

[ -d $HOME/src/dl ] && cdpath+=($HOME/src/dl)
[ -d $HOME/src ] && cdpath+=($HOME/src)

#
# From old directories.zsh
#
unsetopt auto_name_dirs

add_dir () {
    if [ -d $2 ]
    then
        eval "$1=$2"
        : ~$1
    fi
}

add_dir RMSDK ~/src/dl/RMSDK/reader-mobile-sdk
add_dir DLREADER ~/src/dl/RMSDK/reader-mobile-sdk/samples/iOSReferenceViewer
add_dir PDFL9_SANDBOX ~/src/dl/PDFL8/apdfl9-sandbox
add_dir PDFL10_SANDBOX ~/src/dl/PDFL8/apdfl-sandbox
add_dir DLE ~/src/dl/PDFL8/apdfl-sandbox/DLE
add_dir STOPR ~/src/dl/stopr
